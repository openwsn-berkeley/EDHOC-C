cmake_minimum_required(VERSION 3.11)

set(VERSION_MAJOR 0 CACHE STRING "Project major version number")
set(VERSION_MINOR "1" CACHE STRING "Project minor version number")
set(VERSION_PATCH "0" CACHE STRING "Project patch version number")
set(EDHOC_VERSION "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}")

project("edhoc-c" LANGUAGES C VERSION "${EDHOC_VERSION}")

set(CMAKE_C_STANDARD 11)

# Set the output of the libraries and executables.
set(EXECUTABLE_OUTPUT_PATH ${PROJECT_BINARY_DIR}/bin)
set(LIBRARY_OUTPUT_PATH ${PROJECT_BINARY_DIR}/lib)

set(prefix ${CMAKE_INSTALL_PREFIX})
set(exec_prefix ${CMAKE_INSTALL_PREFIX}/bin)
set(libdir ${CMAKE_INSTALL_PREFIX}/lib)
set(includedir ${CMAKE_INSTALL_PREFIX}/include)

###############################################################################
# OPTIONS
###############################################################################

option(EDHOC_C_BUILD_TESTS "build tests" ON)
option(EDHOC_C_BUILD_EXAMPLES "build examples" ON)
option(BUILD_SHARED_LIBS "Build Shared Libraries" OFF)

if (BUILD_SHARED_LIBS)
    set(BUILD_STATIC_LIBS OFF)
else ()
    set(BUILD_STATIC_LIBS ON)
endif ()

if (NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE "Debug" CACHE STRING "Build type" FORCE)
endif ()

set_property(CACHE CMAKE_BUILD_TYPE PROPERTY STRINGS "Debug" "MinSizeRel, Release")

if (NOT EDHOC_CRYPTO_BACKEND)
    set(EDHOC_CRYPTO_BACKEND "WOLFSSL" CACHE STRING "Backend for cryptography" FORCE)
endif ()

set_property(CACHE EDHOC_CRYPTO_BACKEND PROPERTY STRINGS "MBEDTLS" "WOLFSSL")

if (NOT EDHOC_CBOR_BACKEND)
    set(EDHOC_CBOR_BACKEND "CN_CBOR" CACHE STRING "Backend for CBOR encoding/decoding" FORCE)
endif ()

set_property(CACHE EDHOC_CBOR_BACKEND PROPERTY STRINGS "CN_CBOR" "NANOCBOR")

###############################################################################
# SOURCE FILES
###############################################################################

add_subdirectory(src)

###############################################################################
# ADD TESTS
###############################################################################

if (EDHOC_C_BUILD_TESTS)
    enable_testing()
    # include(CTest)
    add_subdirectory(test)
endif ()

###############################################################################
# ADD EXAMPLES
###############################################################################

if (EDHOC_C_BUILD_EXAMPLES)
    add_subdirectory(examples)
endif ()

###############################################################################
# PRINT CONFIG
###############################################################################

message(STATUS "EDHOC_BUILD_TYPE:...............${CMAKE_BUILD_TYPE}")
message(STATUS "EDHOC_BUILD_TESTS:..............${EDHOC_C_BUILD_TESTS}")
message(STATUS "EDHOC_BUILD_EXAMPLES:...........${EDHOC_C_BUILD_EXAMPLES}")
message(STATUS "EDHOC_SHARED_LIBS:..............${BUILD_SHARED_LIBS}")
message(STATUS "EDHOC_CRYPTO_BACKEND:...........${EDHOC_CRYPTO_BACKEND}")
message(STATUS "EDHOC_CBOR_BACKEND:.............${EDHOC_CBOR_BACKEND}")
